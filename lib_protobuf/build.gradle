plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.protobuf'
}

android {
    compileSdkVersion App.compileSdkVersion

    defaultConfig {
        minSdkVersion App.minSdkVersion
        targetSdkVersion App.targetSdkVersion
        versionCode App.versionCode
        versionName App.versionName
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            proto {
                //指定proto源文件地址
                srcDir 'src/main/protobuf'
                include '**/*.protobuf'
            }
        }
    }

    //proto buffer 协议缓冲区相关配置 用于DataStore
    protobuf {
        protoc {
            //protoc版本参见：https://repo1.maven.org/maven2/com/google/protobuf/protoc/
            artifact = "com.google.protobuf:protoc:3.18.0"
        }
        // Generates the java Protobuf-lite code for the Protobufs in this project. See
        // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
        // for more information.
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option 'lite'
                    }
                }
            }
        }
        //修改生成java类的位置 默认是 $buildDir/generated/source/proto
        generatedFilesBaseDir = "$projectDir/src/main/generated"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    //proto文件编译依赖库
    api "com.google.protobuf:protobuf-javalite:3.18.0"
}